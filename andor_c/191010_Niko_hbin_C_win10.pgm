rem ******************************************************************************************
rem *   FLUPS with horizontal binning. In .TellPosition second comread was taken out. 
rem *	 To change horizontal binning change value for whbin in .measure
rem *   Furthermore, the bg is now always recorded very far left on the stage.																			 *
rem *   14Sept/2018																			 *
rem *	for ANDOR DV420 BU    and with long (2 ns) delay stage
rem *                                   by NPE & MQ																					 *
rem ==========================================================================================
rem The following parameters define, at any time, the status of the measurement.
rem They are described and initialized here.
rem ==========================================================================================
pflag=1										:rem flags printing 
rem .FILE parameters
nflag=0                						:rem flags that filename has been properly entered
name$=""									:rem filename without prefix (m or b) or extention
											:rem (not really needed here; just to complete the list...)
cmt$="no comment"							:rem comment
longcmt$="Date Time ..."					:rem comment with preceeding date & time
rem .DELAY parameters
tzi=0										:rem timezero-indicator (needed for 1st cycle only) 
timeperstep=2/3000							:rem time per step in ps
actpos=165935								:rem actual position; is usually overwritten
zeropos=165935								:rem position for which timezero is defined
	delaytime=(actpos-zeropos)*timeperstep	:rem delaytime in ps
stepwidth=50								:rem stepwidth in fs
	incrementpos=stepwidth/(1000*timeperstep)		:rem increment of position during scan	
negdelay=2.0								:rem range (>=0 !) before timezero in ps
	negdelaypos=zeropos-negdelay/timeperstep					:rem start position for scan
maxdelay=34.0								:rem range     after  timezero in ps
	maxdelaypos=zeropos+maxdelay/timeperstep		:rem end   position for scan
	ff=(maxdelaypos-negdelaypos+1)/incrementpos		:rem number of measurements in a scan
	gosub .fix
	nmeas=gg+1
expotime=0.50								:rem exposure time in s
rem .GOTO parameters
gflag=0   									:rem flags that  gototime has been properly entered
gotopos=0									:rem goto position
gototime=0									:rem goto time, relative to time zero
gototimesteps=0								:rem number of steps corresponding to gototime
rem .MEASURE parameters
mflag=0										:rem flags that a measurement has been performed
bgflag=0									:rem flags that background has been stored
rem .TEST and .PHOTOMETRICS parameters
ntest=50									:rem number of test measurements
ntest3=10
rem ==========================================================================================
rem Initialize PI Intellistage M-521.5iM
handshake(2,0)
baud(2,9600)
terminator(0x0a)
newline(0)
cTP$="1TP"+chr$(10) :  cGL$="1GL"+chr$(10) 
cSVfast$="1SV100000"+chr$(10)
cSVslow$="1SV20000"+chr$(10)
rem
targetpos=-999999							:rem move extremely far to the left until limit
gosub .MoveAbsoluteFast
if vGL<> 2 then
	print("Error: stage stopped first  time but limit switch not activated")
	gosub .DefineHome
	gosub .MoveAbsoluteFast
	if vGL<> 2 then
		print("Error: stage stopped second time but limit switch not activated")
		gosub .DefineHome
	endif
endif
gosub .DefineHome							:rem define home at left limit
print("setting actpos=";actpos)
key("Home has been defined at left border; ......Press enter......")
rem
rem
targetpos=165935							:rem go towards right
gosub .MoveAbsoluteFast						
key("After having moved 100 ps to the right......                    Press enter......")
rem
rem Top level
rem
idummy=1
while (idummy==1)  
	gosub .main
	if (pflag==1) then
		print("**  STATUS *************************************************************************")
		print("tzi=";tzi;",     zeropos=";zeropos;",      actpos=";actpos;",      delaytime =";delaytime;" ps")
		print("stepwidth=";stepwidth;" fs,      incrementpos=";incrementpos)
		print("      negdelay=";negdelay;" ps,      negdelaypos=";negdelaypos;",      maxdelay=";maxdelay;" ps,      maxdelaypos=";maxdelaypos)
		print("      nmeas=";nmeas;",      expotime="; expotime;" s")
		print("gototimesteps=";gototimesteps;",      gototime=";gototime;" ps")
		print("fname=";fname$;",      longcmt=";longcmt$)
	endif
wend
rem
rem ******************************************************************************************
.main
rem ******************************************************************************************
rem selects the submenue
rem ------------------------------------------------------------------------------------------
mi=0
while mi==0
input(" * Main *    delay ";delaytime;" ps   at position "; actpos;"           Del, Go, Tst, Meas, Scan, Phot, File, Exit ?",answ$) 
	if (asc(answ$)==68) || (asc(answ$)==100) then 
		gosub .delay 
		mi=1
	endif
	if (asc(answ$)==71) || (asc(answ$)==103) then 
		gosub .goto
		mi=1
	endif
	if (asc(answ$)==84) || (asc(answ$)==116) then 
		gosub .test
		mi=1
	endif
	if (asc(answ$)==77) || (asc(answ$)==109) then 
		gosub .measure
		mi=1
	endif
	if (asc(answ$)==70) || (asc(answ$)==102) then 
		gosub .file
		mi=1
	endif
	if (asc(answ$)==69) || (asc(answ$)==101) then 
		gosub .exit
		mi=1
	endif
	if (asc(answ$)==83) || (asc(answ$)==115) then 
		gosub .measure2								:rem scan      a preprogrammed delay range
		mi=1
	endif
	if (asc(answ$)==80) || (asc(answ$)==112) then 
		gosub .measure3								:rem photometrics: background & last delay
		mi=1
	endif
wend
return
rem
rem ******************************************************************************************
.delay
rem ******************************************************************************************
rem inputs, checks, and updates
rem zeropos, (if that is changed:) delaytime,stepwidth,negdelay,maxdelay
rem ------------------------------------------------------------------------------------------
gosub .TellPosition
actpos=vTP : delaytime=(actpos-zeropos)*timeperstep
input(" ***** Delay *****    delay ";delaytime;" ps at position "; actpos;"             Set timezero here (0 or 1)?",answ)
if answ==0 then tzi=0   :rem tzi flag: the actual position becomes new home
if answ<>0 then tzi=1
if tzi==1 then
	shiftpos=actpos-zeropos		
	maxdelaypos=maxdelaypos+shiftpos
	negdelaypos=negdelaypos+shiftpos
	zeropos=actpos: delaytime=0.0 
	gotopos=zeropos+gototimesteps
endif 
rem
answ=0
input("Step width          (fs)   ";stepwidth;"  ",answ)
if answ<>0 then 
	if abs(answ)<3 then answ=3
	stepwidth=abs(answ)
	if (3<=stepwidth) && (stepwidth<10) then
		negdelay=0.21:maxdelay=2.3
		negdelaypos=zeropos-negdelay/timeperstep
		maxdelaypos=zeropos+maxdelay/timeperstep	
	endif
	if (10<=stepwidth) && (stepwidth<30) then
		negdelay=0.50:maxdelay=6.5
		negdelaypos=zeropos-negdelay/timeperstep
		maxdelaypos=zeropos+maxdelay/timeperstep			
	endif	 
	if (30<=stepwidth) && (stepwidth<50) then
		negdelay=1.80:maxdelay=22.0
		negdelaypos=zeropos-negdelay/timeperstep
		maxdelaypos=zeropos+maxdelay/timeperstep			
	endif
	if (50<=stepwidth) && (stepwidth<100) then
		negdelay=2.00:maxdelay=34.0
		negdelaypos=zeropos-negdelay/timeperstep
		maxdelaypos=zeropos+maxdelay/timeperstep			
	endif	
	if (100<=stepwidth) && (stepwidth<200) then
		negdelay=4.00:maxdelay=70.0
		negdelaypos=zeropos-negdelay/timeperstep
		maxdelaypos=zeropos+maxdelay/timeperstep			
	endif
	if (200<=stepwidth) && (stepwidth<500) then
		negdelay=4.00:maxdelay=126.0
		negdelaypos=zeropos-negdelay/timeperstep
		maxdelaypos=zeropos+maxdelay/timeperstep			
	endif
	ff=stepwidth/(1000*timeperstep)
	gosub .fix
	incrementpos=gg
endif
rem
answ=0
input("Negative range  (ps)   ";negdelay;"  ",answ)
if answ<>0 then 
	negdelay=abs(answ)
	negdelaypos=zeropos-negdelay/timeperstep	:rem start position for scan
endif
rem
answ=0
input("Maximum  delay (ps)   ";maxdelay;"  ",answ)
if answ<>0 then 
	maxdelay=abs(answ)
	maxdelaypos=zeropos+maxdelay/timeperstep	:rem end   position for scan
endif
rem
	ff=(maxdelaypos-negdelaypos+1)/incrementpos	:rem number of measurements in a scan
	gosub .fix
	nmeas=gg+1
rem
answ=0
input("Exposure time   (s)   ";expotime;"  ",answ)
if answ<>0 then expotime=abs(answ)
return
rem
rem ******************************************************************************************
.file
rem ******************************************************************************************
rem Inputs filename, saves #100 and #99
rem ------------------------------------------------------------------------------------------
mit$="m"
bit$="b"
ext$=".sif"
x$=date$()
day$=left$(x$,2)+mid$(x$,4,2)+right$(x$,2)
rem
if (mflag==0) then
	print("no data available yet ...")
else
rem
	if (nflag==0) then
		name$=""
		while (len(name$)==0)
			input("  ***** File *****                                                                        Enter filename without .ext ",name$)
rem			if (len(name$)==0) then beep()
		wend
			input("  ***** File *****                                                                        Comment = ";cmt$;"; overwrite...", answ$) 
			if (len(answ$)<>0) then cmt$=answ$
			fname$=mit$+name$+ext$ : bname$=bit$+name$+ext$ : nflag=1
	else
		input("  ***** File *****                                                                            Enter filename without .ext   (";name$;")   ",answ$)
			if (len(answ$)==0) then
			fname$=mit$+name$+ext$ : bname$=bit$+name$+ext$
			else 
			name$=answ$ : fname$=mit$+name$+ext$ : bname$=bit$+name$+ext$
			endif
		input("  ***** File *****                                                                            Comment = ";cmt$;"; overwrite...", answ$) 
			if (len(answ$)<>0) then cmt$=answ$
			fname$=mit$+name$+ext$ : bname$=bit$+name$+ext$
	endif
	print("Now saving signal and background to "; fname$;" and ";bname$)
	dt$= date$(): tm$=time$()
	longcmt$=dt$+" "+tm$+" "+cmt$
	infotext(#100,longcmt$) : infotext(#99,longcmt$) 
	save(#100,fname$)
	save(#99,bname$)
rem
endif
return
rem ******************************************************************************************
.exit
rem ******************************************************************************************
input("  ***** Exit *****                                                                               enter 1 if you are sure... ", answ)
if (answ==1) then 
	minimizewindow(#100): minimizewindow(#99) : minimizewindow(#0)
	end
endif
return
rem ******************************************************************************************
.TellPosition
rem ******************************************************************************************
rem updates vTP
rem ------------------------------------------------------------------------------------------
comwrite(2,cTP$)
delay(50)
comread(2,a$)
comread(2,a$)
vTP = val(right$(a$,len(a$)-4))
return
rem ******************************************************************************************
.SenseLimit
rem ******************************************************************************************
comwrite(2,cGL$)
delay(50)
comread(2,a$)
comread(2,a$)
vGL=val(right$(a$,len(a$)-4))
return
rem ******************************************************************************************
.MoveAbsoluteFast
rem ******************************************************************************************
rem moves stage fast to targetpos 
rem ------------------------------------------------------------------------------------------
if ((targetpos-mod(targetpos,1000000))/1000000)>0 then
 
 cMA$="1MA"+str$((targetpos-mod(targetpos,10))/10)+str$(mod(targetpos,10))+chr$(10)
 else 
   cMA$="1MA"+str$(targetpos)+chr$(10)
endif
print("targetpos",targetpos, cMA$)
gosub .TellPosition									:rem get the position directly after startup
vTPprev=vTP	
comwrite(2,cSVfast$) : delay(50) : comread(2,a$)	
comwrite(2,cMA$)     : delay(50) : comread(2,a$)	
tpflag=0											:rem wait until stage has come to rest
while tpflag==0
	gosub .TellPosition
	if (vTP<>vTPprev) then
		vTPprev=vTP
	else
		tpflag=1
	endif
wend
if (vTP==targetpos) then
	if (pflag==1) then print("Stage now at target position ";targetpos)
	actpos=vTP : delaytime=(actpos-zeropos)*timeperstep
else
	if (pflag==1) then print("Stage stopped moving at position ";vTP)
	gosub .SenseLimit
	if (pflag==1) then print("Limit status is ";vGL)
	actpos=vTP : delaytime=(actpos-zeropos)*timeperstep
endif	
return
rem ******************************************************************************************
.MoveAbsoluteSlow
rem ******************************************************************************************
rem moves stage slow to targetpos 
rem ------------------------------------------------------------------------------------------
if ((targetpos-mod(targetpos,1000000))/1000000)>0 then
 
 cMA$="1MA"+str$((targetpos-mod(targetpos,10))/10)+str$(mod(targetpos,10))+chr$(10)
 else 
   cMA$="1MA"+str$(targetpos)+chr$(10)
endif	
gosub .TellPosition									:rem get the position directly after startup
vTPprev=vTP	
comwrite(2,cSVslow$) : delay(50) : comread(2,a$)	
comwrite(2,cMA$)     : delay(50) : comread(2,a$)	
tpflag=0											:rem wait until stage has come to rest
while tpflag==0
	gosub .TellPosition
	if (vTP<>vTPprev) then
		vTPprev=vTP
	else
		tpflag=1
	endif
wend
if (vTP==targetpos) then
	if (pflag==1) then print("Stage now at target position ";targetpos)
	actpos=vTP : delaytime=(actpos-zeropos)*timeperstep
else
	if (pflag==1) then print("Stage now at limit  position ";vTP)
	actpos=vTP : delaytime=(actpos-zeropos)*timeperstep
endif
return
rem ******************************************************************************************
.DefineHome
rem ******************************************************************************************
rem defines home on stage &  resets   zeropos, actpos
rem ------------------------------------------------------------------------------------------
cDH$="1DH"+chr$(10)
comwrite(2,cDH$)
delay(50)
comread(2,a$)
gosub .TellPosition
actpos=vTP
return
rem ******************************************************************************************
.goto
rem ******************************************************************************************
rem inputs, checks, and updates    gototime & gotopos
rem and moves there fast
rem ------------------------------------------------------------------------------------------
gosub .TellPosition									:rem get the position directly after startup
actpos=vTP	
delaytime=(actpos-zeropos)*timeperstep
rem...
gti=0
while gti==0
	input(" ***** Goto *****        delay ";delaytime;" ps at position ";actpos;"                           Enter desired delay (ps; or z)   ";gototime;"  ",answ$)
	if (asc(answ$)==122) || (asc(answ$)==90) then	:rem "z" was entered to go home
		gotopos=zeropos : gototime=0
		targetpos=gotopos
		gosub .MoveAbsoluteSlow
		gosub .TellPosition							:rem get the position directly after startup
		actpos=vTP	
		delaytime=(actpos-zeropos)*timeperstep
		gti=1
	else								
		if (asc(answ$)==0) then						:rem 0 or return  was entered indicating no change									
			targetpos=gotopos
			gosub .MoveAbsoluteSlow
			gosub .TellPosition						
			actpos=vTP	
			delaytime=(actpos-zeropos)*timeperstep
			gti=1
		else
			aa=val(answ$)							:rem some number was entered ...
			ff=aa/timeperstep						:rem ....possibly some broken number of steps
			gosub .fix
			gototimesteps=gg
			xx=zeropos+gototimesteps
			
			Print("aa",aa," ff",ff, " gg", gg, "xx", xx, str$(xx))
			if (0 <=xx) && (xx<=2000000)  then		:rem move is accepted
				gotopos=xx		
				gototime=gototimesteps*timeperstep	:rem correct gototime commensurate with steps
				targetpos=xx
				gosub .MoveAbsoluteSlow
				gosub .TellPosition					
				actpos=vTP	
				delaytime=(actpos-zeropos)*timeperstep
				gti=1
			else 
				gti=0
			endif									:rem retry input...
		endif
	endif
wend 
return
rem ******************************************************************************************
.fix
rem ******************************************************************************************
rem a number on  ff is clipped & the corresponding integer placed on gg 
rem ------------------------------------------------------------------------------------------
rem f$=str$(ff)
rem a =instr(f$,".")
rem if a then
rem   name$=left$(f$,a-1)
rem else
rem name$=f$
rem endif
rem gg=val(name$)
gg=ff-mod(ff,1)
return
rem
rem ******************************************************************************************
.measure
rem ******************************************************************************************
rem move to start position, measure background there, and scan the full delay range 
rem ------------------------------------------------------------------------------------------
rem Setup of the camera
	setacquisitiontype(0)					:rem signal
	setacquisitionmode(1)        			:rem single scan mode
rem	setreadoutmode(0)            			:rem chose either Full Vertically Binned (FVB) spectra
	whbin=2
	setfvb(whbin)
rem	setsingletrack(140,80)               	:rem       or else single track binning
	setdatatype(1)							:rem counts
	setReadoutTime(32)										:rem slow horizontal readout
rem
	create(#98,1024/whbin,1,5)					:rem 1024x, 1y dimensioned intermediate bg set of 5 items
	create(#99,1024/whbin,1,1)            			:rem 1024x, 1y dimensioned background      set of 1 item
	minimizewindow(#98)	
	minimizewindow(#99)						:rem shrink window to icon
	create(#100,1024/whbin,1,nmeas)				:rem 1024 x, 1 y dimensioned  signal    set of nmeas items
	maximizewindow(#100)					:rem 
	ScaleData(#100,-50,1000)                			:rem scale at start from -50 to 200 counts
	positioncursor(#100,50): cursxprev=50			:rem sets and remembers cursor x rem Starting position and background 
rem     								:rem move to the start position for the scan
	negdelaypos=zeropos-negdelay/timeperstep 		:rem start position for scan
	targetpos=negdelaypos					:rem move to the start position for the scan
	gosub .MoveAbsoluteFast
	print("Now at bg position  ";actpos)        			:rem the bg is made almost at the start of the scan
	setexposuretime(0.021)					:rem for cleansing readout
	run()								:rem cleansing readout
	setexposuretime(expotime)
rem
	bgcounter=1
	while bgcounter<=5						:rem background measurements
		run()
		#0[1]=0								:rem pixel 1 is set to 0
		activekineticposition(#0,bgcounter)
		#98{bgcounter}=#0
		bgcounter=bgcounter+1
		delay(200)
	wend
rem	
	#99=(#98_sig{1}+#98_sig{2}+#98_sig{3}+#98_sig{4}+#98_sig{5})/5
rem #99=smooth3(#99)                        :rem perform 9 point Savitsky-Golay smoothing on bg spectrum
	
	bgflag=1			
	topwindow(#100)
	targetpos=negdelaypos   :rem go back to the start of the scan
	gosub .MoveAbsoluteFast				
rem -------------------------------------------- scan ----------------------------------------
pflag=0										:rem switches off report from MoveAbsolute...
counter=1
targetpos=negdelaypos-incrementpos
while (counter<=nmeas) && (delaytime <=	maxdelay)
	targetpos=targetpos+incrementpos
	gosub .MoveAbsoluteSlow
	print(counter;"/";nmeas;" at ";actpos; " & ";delaytime;" ps")
	infotext(#100,"/";nmeas;" at ";actpos; " & ";delaytime;" ps")
	run()									:rem measurement at the present delay
	#0[1]=delaytime							:rem place counter  onto pixel 1
	#100{counter}=#0-#99					:rem subtract background and place item into dataset #100
	activekineticposition(#100,counter)
	gosub .meascursor
	counter=counter+1
wend
	targetpos=zeropos
	gosub .MoveAbsoluteFast
	print("Now at zero position  ";actpos)
mflag=1										:rem flags that measurement has been done
return
rem
rem ******************************************************************************************
.measure2									:rem = "scan"
rem ******************************************************************************************
rem move to fixed neg. delay, measure background there, and scan a preprogrammed delay range 
rem ------------------------------------------------------------------------------------------
rem Setup of the camera
	setacquisitiontype(0)					:rem signal
	setacquisitionmode(1)        			:rem single scan mode
rem	setreadoutmode(0)            			:rem chose either Full Vertically Binned (FVB) spectra
	whbin=2
	setfvb(whbin)
rem	setsingletrack(140,80)               	:rem       or else single track binning
	setdatatype(1)				:rem counts
	setReadoutTime(32)										:rem slow horizontal readout
rem
	create(#98,1024/whbin,1,5)					:rem 1024x, 1y dimensioned intermediate bg set of 5 items
	create(#99,1024/whbin,1,1)            		:rem 1024x, 1y dimensioned background      set of 1 item
	minimizewindow(#98)	
	minimizewindow(#99)						:rem shrink window to icon
	create(#100,1024/whbin,1,260)					:rem 1024 x, 1 y dimensioned  signal    set of 260 items
	maximizewindow(#100)					:rem 
	ScaleData(#100,-50,1000)                :rem scale at start from -50 to 200 counts
	positioncursor(#100,50): cursxprev=50	:rem sets and remembers cursor x rem Starting position and background 
	
	negdelay2=10
	negdelaypos2=zeropos-negdelay2/timeperstep 		:rem start position for scan
	targetpos=negdelaypos2					:rem move to the start position for the scan
	gosub .MoveAbsoluteFast
	print("Now at start position  ";actpos)
	setexposuretime(0.021)					:rem for cleansing readout
	run()									:rem cleansing readout
	setexposuretime(expotime)
rem
	bgcounter=1
	while bgcounter<=5						:rem background measurements
		run()
		#0[1]=0								:rem pixel 1 is set to 0
		activekineticposition(#0,bgcounter)
		#98{bgcounter}=#0
		bgcounter=bgcounter+1
		delay(200)
	wend
rem	
	#99=(#98_sig{1}+#98_sig{2}+#98_sig{3}+#98_sig{4}+#98_sig{5})/5
rem #99=smooth3(#99)                        :rem perform 9 point Savitsky-Golay smoothing on bg spectrum
	bgflag=1			
	topwindow(#100)				
rem -------------------------------------------- scan ----------------------------------------
	pflag=0										:rem switches off report from MoveAbsolute...
	counter=1
	incrementpos2=100/(1000*timeperstep)			:rem initial increment (in fs) of position during scan
	nmeas=130									:rem depends on programmed delay positions!

	negdelay2=5
	negdelaypos2=zeropos-negdelay2/timeperstep	:rem start position for scan
	targetpos=negdelaypos2-incrementpos2
gosub .MoveAbsoluteFast

while (counter<=nmeas) && (delaytime <= 62)
	targetpos=targetpos+incrementpos2
	gosub .MoveAbsoluteSlow
	print(counter;"/";260;" at ";actpos; " & ";delaytime;" ps")
	infotext(#100,"/";260;" at ";actpos; " & ";delaytime;" ps")
	run()									:rem measurement at the present delay
	#0[1]=delaytime							:rem place counter  onto pixel 1
	#100{counter}=#0-#99					:rem subtract background and place item into dataset #100
	activekineticposition(#100,counter)
	gosub .meascursor
	if (counter<=100) then
		incrementpos2=100/(1000*timeperstep)
	endif
	if (counter<=175) && (counter>100) then
		incrementpos2=200/(1000*timeperstep)
	endif
	if (counter<=259) && (counter>175) then
		incrementpos2=500/(1000*timeperstep)
	endif
	counter=counter+1
wend
	targetpos=zeropos
	gosub .MoveAbsoluteFast
	print("Now at zero position  ";actpos)
mflag=1										:rem flags that measurement has been done
return 
rem ******************************************************************************************
.measure3									:rem = "photometrics"
rem ******************************************************************************************
rem move to start position, measure background, go to end position, make ntest3 measurements
rem ------------------------------------------------------------------------------------------
rem Setup of the camera
	setacquisitiontype(0)					:rem signal
	setacquisitionmode(1)        			:rem single scan mode
	whbin=2
	setfvb(whbin)
rem	setreadoutmode(0)            			:rem chose either Full Vertically Binned (FVB) spectra
rem	setsingletrack(140,80)               	:rem       or else single track binning
	setdatatype(1)							:rem counts
	setReadoutTime(32)										:rem slow horizontal readout
rem
	create(#98,1024/whbin,1,5)					:rem 1024x, 1y dimensioned intermediate bg set of 5 items
	create(#99,1024/whbin,1,1)            		:rem 1024x, 1y dimensioned background      set of 1 item
	minimizewindow(#98)	
	minimizewindow(#99)						:rem shrink window to icon
	create(#100,1024/whbin,1,ntest3)				:rem 1024 x, 1 y dimensioned  signal    set of ntest3 items
	maximizewindow(#100)					:rem 
	ScaleData(#100,-50,1000)                :rem scale at start from -50 to 200 counts
	positioncursor(#100,50): cursxprev=50	:rem sets and remembers cursor x rem Starting position and background 
rem
	negdelaypos=zeropos-negdelay/timeperstep 		:rem start position for scan
	targetpos=negdelaypos					:rem move to the start position for the scan
	gosub .MoveAbsoluteFast
	print("Now at start position  ";actpos)
	setexposuretime(0.021)					:rem for cleansing readout
	run()									:rem cleansing readout
	setexposuretime(expotime)
rem
	bgcounter=1
	while bgcounter<=5						:rem background measurements
		run()
		#0[1]=0								:rem pixel 1 is set to 0
		activekineticposition(#0,bgcounter)
		#98{bgcounter}=#0
		bgcounter=bgcounter+1
		delay(200)
	wend
rem	
	#99=(#98_sig{1}+#98_sig{2}+#98_sig{3}+#98_sig{4}+#98_sig{5})/5
rem #99=smooth3(#99)                        :rem perform 9 point Savitsky-Golay smoothing on bg spectrum
	bgflag=1			
	topwindow(#100)				
rem -------------------------------------------- scan ----------------------------------------
pflag=0										:rem switches off report from MoveAbsolute...
	targetpos=maxdelaypos					:rem move to the end position for a scan
	gosub .MoveAbsoluteFast
	print("Now at end position  ";actpos)
	setexposuretime(0.021)					:rem for cleansing readout
	run()									:rem cleansing readout
	setexposuretime(expotime)
counter=1
while (counter<=ntest3) 
	print(counter;"/";ntest3;" at ";actpos; " & ";delaytime;" ps")
	infotext(#100,counter;"/";ntest;" at ";actpos; " & ";delaytime;" ps")
	run()									:rem measurement at the present delay
	#0[1]=delaytime							:rem place counter  onto pixel 1
	#100{counter}=#0-#99					:rem subtract background and place item into dataset #100
	activekineticposition(#100,counter)
	gosub .testcursor
	counter=counter+1
	minimizewindow(#0)
wend
targetpos=zeropos
	gosub .MoveAbsoluteFast
	print("Now at zero position  ";actpos)
mflag=1										:rem flags that measurement has been done
return
rem
rem
rem ******************************************************************************************
.test
rem ******************************************************************************************
rem stays at actual delay and records ntest  signal spectra
rem ------------------------------------------------------------------------------------------
If (bgflag==0) then
	key("Background not measured yet...")
else
rem Setup of the camera
	setacquisitiontype(0)					:rem signal
	setacquisitionmode(1)        			:rem single scan mode
	whbin=2
	setfvb(whbin)
rem	setreadoutmode(0)            			:rem chose either Full Vertically Binned (FVB) spectra
rem    setsingletrack(125,75)           		:rem       or else single track binning	setreadoutmode(0)            			:rem Full Vertically Binned (FVB) spectra
	setdatatype(1)							:rem counts
	setReadoutTime(32)										:rem slow horizontal readout
rem
	minimizewindow(#98)	
	minimizewindow(#99)						:rem shrink window to icon
	create(#100,1024/whbin,1,ntest)				:rem 1024 x, 1 y dimensioned  signal    set of nmeas items
rem	maximizewindow(#100)					:rem 
rem	topwindow(#100)
	positioncursor(#100,50): cursxprev=50	:rem sets and remembers cursor x 	rem Starting position and background 
	setexposuretime(0.021)					:rem for cleansing readout
	run()									:rem cleansing readout
	setexposuretime(expotime)
rem -------------------------------------------- scan ----------------------------------------
pflag=0										:rem switches off report from MoveAbsolute...
counter=1
while (counter<=ntest) 
	print(counter;"/";ntest;" at ";actpos; " & ";delaytime;" ps")
	infotext(#100,counter;"/";ntest;" at ";actpos; " & ";delaytime;" ps")
	run()									:rem measurement at the present delay
	#0[1]=delaytime							:rem place counter  onto pixel 1
	#100{counter}=#0-#99					:rem subtract background and place item into dataset #100
	activekineticposition(#100,counter)
	gosub .testcursor
	counter=counter+1
	minimizewindow(#0)
wend
mflag=1										:rem flags that measurement has been done
endif
return
rem ******************************************************************************************
.meascursor
rem ******************************************************************************************
rem checks movement of cursorx
rem ------------------------------------------------------------------------------------------
	cursxnow=cursorx(#100)
	if (cursxnow<>cursxprev) then			:rem cursor x was moved
		if (cursxnow>cursxprev) then		:rem double the actual stepwidth w ithout changing 
											:rem           corresp. entry  in delay parameters
			incrementpos=2*incrementpos
			cursxprev=cursxnow
		endif
		if (cursxnow<cursxprev) then
			counter=nmeas+1
		endif
	endif									:rem cursor x was not moved
	return
rem ******************************************************************************************
.testcursor
rem ******************************************************************************************
rem checks movement of cursorx
rem ------------------------------------------------------------------------------------------
	cursxnow=cursorx(#100)
	if (cursxnow<>cursxprev) then			:rem cursor x was moved	
			counter=ntest+1
	
	endif									:rem cursor x was not moved
	return